pkgname=intel-ipu6-dkms-git
_pkgname=ipu6-drivers
pkgver=r70.913905fe0
pkgrel=1
pkgdesc="Intel IPU6 camera drivers (DKMS)"
arch=('any')
url="https://github.com/intel/ipu6-drivers"
revision="913905fe01aa11219b121cf016a892c1654dec29"
license=('unknown')
depends=('dkms')
makedepends=('git')
source=("git+${url}.git#commit=${revision}"
	"0001-Fix-compilation-with-kernel-6.1.patch"
	"0002-ipu-isys-queue-Fix-NULL-pointer-deref-with-kernels-6.patch")
sha256sums=('SKIP'
		'815345cde327e873f1ee569b476ecf78a00d0aeb737e84147bf5e7fcc01c00bb'
		'ca4e48583457f20b32d483dff9e5d880027898efac4f9248e68ee8d92c395187')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -p1 < "${srcdir}/0001-Fix-compilation-with-kernel-6.1.patch"
    patch -p1 < "${srcdir}/0002-ipu-isys-queue-Fix-NULL-pointer-deref-with-kernels-6.patch"
    git clone https://github.com/intel/ivsc-driver.git
    cp -r ivsc-driver/backport-include ivsc-driver/drivers ivsc-driver/include .
    rm -rf ivsc-driver
    sed -i "s/^PACKAGE_VERSION=\".*\"$/PACKAGE_VERSION=\"$pkgver\"/g" ./dkms.conf
}

package() {
    cd "$srcdir"
    install -dm755 "$pkgdir/usr/src"
    cp -rT "$_pkgname" "$pkgdir/usr/src/$_pkgname-$pkgver"
}
